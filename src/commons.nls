;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Fonctions communes aux différents agents ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


; Rotation de l'agent vers sa cible, sans dépasser un angle limite pour garder un certain réalisme
to rotate-agent [ goal ]
  ; Si le drone se trouve à l'endroit souhaité (modulo l'altitude)
  if is-turtle? goal and [xcor] of goal = xcor and [ycor] of goal = ycor or 
  is-patch? goal and [pxcor] of goal = xcor and [pycor] of goal = ycor [
    stop
  ]
  
  ; Calcul de l'angle avec la cible
  let headingFlag (towards-nowrap goal)
  let dirCorrection subtract-headings headingFlag heading
  
  ; Arrondissement de l'angle (on ne veut pas faire de micro tournant)
  set dirCorrection precision dirCorrection 2
  
  ; Limite de l'angle, pour que ce soit plus realiste (l'ennemi ne peut pas faire un demi-tour sur place!)
  ifelse dirCorrection > max-dir [ ; limite a droite
    set dirCorrection max-dir
  ]
  [
    if dirCorrection < max-dir * -1 [ ; limite a gauche
      set dirCorrection max-dir * -1
    ]
  ]
  
  ; On tourne
  rt dirCorrection
end


;;;;;;;;;;;;;;;;;;;;
;; Communications ;;
;;;;;;;;;;;;;;;;;;;;

to-report check-message [msg performative content] 
  report msg != "no_message" and get-performative msg = performative and get-content msg = content
end

to-report create-complete-msg [receiver performative content]
  let msg create-message performative
  set msg add-receiver receiver msg
  set msg add-content content msg
  report msg
end

to-report create-complete-reply [msg performative content]
  let reply create-reply performative msg
  set reply add-content content reply 
  report reply
end

to-report get-msg-if-performative [performative]
  let msg get-message-no-remove
  if get-performative msg = performative [ report get-message ]
  report nobody
end