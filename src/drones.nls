;;;;;;;;;;;;;;;;;;;;;;;;;
;; Drones related code ;;
;;;;;;;;;;;;;;;;;;;;;;;;;

breed [drones drone]

drones-own [
  ;; Statut du drone
  ;; ---
  ;; • statu                      : Le poste que doit tenir le drone parmis
  ;;    • protect-convoi          : Le drone protege un convoie
  ;;    • attaquant               : Le drone attaque les ennemis
  ;;    • ravitailleur            : Le drone ravitaille les autres drones
  ;;    • surveillant             : Le drone surveille le terrain
  ;;
  ;; • initial-health             : Nombre de points de vie initiaux du drone
  ;; • health                     : Nombre de points de vie restants au drone
  ;; • leader?                    : Grade du drone
  ;; • dead?                      : Vitalité du drone
  ;; • isAttacked                 : Indique si le drone est attaqué
  ;; • vision-radius              : Taille du champs de vision
  
  ;; • intentions                 : Liste des intentions du drone, pour la BDI
  ;; • incoming-queue             : Liste des messages reçus
  ;; 
  ;; • K-drones-updated           : Indique s'il y a eu une modification apporter à ses connaissances
  ;; • last-role                  : Role du drone au dernier tick
  ;; • last-isAttacked            : Indique si le drone était attaqué au dernier tick
  ;; • last-stat-of-fuel          : Donne l'état du niveau de carburant du dernier tick (cf. partage-connaissances.nls)
  statu
  initial-health
  health
  leader?
  dead?
  isAttacked
  vision-radius
  
  intentions
  incoming-queue
  
  K-drones-updated
  last-role
  last-isAttacked
  last-stat-of-fuel
  

  ;; Beliefs
  ;; ---
  ;;   Connaissances
  ;;   ---
  ;;   • K-Infos-Drones           : Liste des connaissances sur les autres drones
  ;;   • K-Infos-Convois          : Liste des connaissances sur les autres convois
  ;;   Suivi
  ;;   ---
  ;;   • target                   : Convoi que le drone suit et protège
  ;;   • convoi-already-protected : Liste des convois qui ont une protection suffisante
  ;;   • drone-protect leader     : Leader du groupe de protection
  ;;   • drone-protect team       : Equipe de protection
  ;;   Rapatriement
  ;;   ---
  ;;   • base-patch               : Position de la base
  ;;   • last-statu               : Statu avant le rapatriement
  beliefs

  ;; Déplacement
  ;; ---
  ;; • speed            : Vitesse du drone
  ;; • max-dir          : Rotation maximale par tick
  ;; • max-alt          : Altitude de vol maximale
  ;; • min-alt          : Altitude de vol minimale
  ;; • my-paht          : Chemin que doit suivre le drone
  speed
  max-dir
  max-alt
  min-alt
  my-path
  
  ;; Carburant
  ;; ---
  ;; • fuel             : Carburant restant
  ;; • max-fuel         : Taille du réservoire
  ;; • pourcentage-sec. : Pourcentage de fuel suplémentaire estimé comme nécessaire pour un Rapatriement
  fuel
  max-fuel
  pourcentage-securite-fuel-drones
]

;;;;;;;;;;;
;; SETUP ;;
;;;;;;;;;;;

to setup-drones
  create-drones nb-drones
  load-shapes-3d "ressources/drone.nlshape"
  
  let tmp-nb-protector nb-protector
  let tmp-nb-explorator nb-explorator
  
  ask drones [
    ; Init apparence NetLogo
    set shape "turtle"
    set size size * 0.03
    ;set color grey - 2
    
    ;; Variables
    ;; ---
    ;; Statut du drone
    set statu "protect-convoi"
    set initial-health max-health-drones
    set health initial-health
    set leader? false
    set dead? false
    set vision-radius drones-vision-radius
    
    set intentions ([])
    set incoming-queue []
    
    set K-drones-updated false
    set last-isAttacked false
    
    ;; Beliefs
    ;; ---
    ;;   Connaissances
    ;;   ---
    ;;   • K-Infos-Drones            : Liste des connaissances sur les autres drones
    ;;   • K-Infos-Convois           : Liste des connaissances sur les autres convois
    ;;   Suivi
    ;;   ---
    ;;    • target                   : Convoi que le drone suit et protège
    ;;    • convoi-already-protected : Liste des convois qui ont une protection suffisante
    ;;    • etat enrollement         : Etape dans la procédure de recrutement
    ;;    • counter enroll.          : Compteur servant au TimeOut pour la procédure de recrutement
    ;;   Rapatriement
    ;;   ---
    ;;    • base-patch               : Position de la base
    ;;    • last-statu               : Statu avant le rapatriement
    set beliefs ([])
    setup-drones-beliefs
    
    ;; Deplacement
    set speed speed-drones * simu-speed / 1000
    set max-dir 10 * simu-speed
    set max-alt max-alt-drones + solAlt
    set min-alt min-alt-drones + solAlt
    set my-path []
    
    ;; Carburant                             --- Plusieurs modèles de drones avec des caractéristiques pré-définis ?
    set fuel max-fuel-drones
    set max-fuel max-fuel-drones
    set pourcentage-securite-fuel-drones 0.2
    set last-stat-of-fuel floor (fuel / fuel-estimate)
    
    ;; Placement
    move-to one-of patches with [ base? ]
    set zcor solAlt
    ifelse random 2 = 0
    [ set heading 0 ]
    [ set heading 90 ]
    
    ;; Stratégie
    ifelse tmp-nb-protector > 0
    [
      add-intention "cherche-convoi-drones" "cherche-convoi-drones-done"
      add-intention "take-off-drones" "take-off-drones-done"
      set last-role intention-name get-intention
      set tmp-nb-protector tmp-nb-protector - 1
    ]
    [
      if tmp-nb-explorator > 0
      [
        intelligent-update-belief "etat terrain" setup-etat-terrain-drones
        add-intention "explore-drones" "explore-drones-done"
        add-intention "take-off-drones" "take-off-drones-done"
        set last-role intention-name get-intention
        set tmp-nb-explorator tmp-nb-explorator - 1
      ]
    ]
  ]
end

to setup-drones-beliefs
  intelligent-update-belief "role" "drone-protect"
  intelligent-update-belief "K-Infos-Drones" []
  intelligent-update-belief "K-Infos-Convois" []
  intelligent-update-belief "target" nobody
  intelligent-update-belief "convoi-already-protected" []
  intelligent-update-belief "etat enrollement" "cherche convoi"
  intelligent-update-belief "counter enrollement" 0
  intelligent-update-belief "drone-protect leader" nobody
  intelligent-update-belief "drone-protect team" []
  
  intelligent-update-belief "old-target" nobody
  intelligent-update-belief "explorator-check-counter" 0
  
  intelligent-update-belief "base-patch" nobody
  intelligent-update-belief "last-statu" "enBase"
  intelligent-update-belief "etat terrain" setup-etat-terrain-drones
  intelligent-update-belief "explorer center" nobody
end

to soft-reset-drones
  ask drones [
    ; Init apparence NetLogo
    
    ;; Variables
    ;; ---
    ;; Statut du drone
    set statu "protect-convoi"
    set intentions ([])
    set incoming-queue []
    
    set dead? false
    set vision-radius drones-vision-radius
    
    ;; Beliefs
    ;; ---
    ;;   Suivi
    ;;   ---
    ;;    • target               : Convoi que le drone suit et protège
    ;;    • etat enrollement     : Etape dans la procédure de recrutement
    ;;    • counter enroll.      : Compteur servant au TimeOut pour la procédure de recrutement
    ;;    • drone-protect leader : Leader du groupe de protection
    ;;    • drone-protect team   : Equipe de protection
    ;;   Rapatriement
    ;;   ---
    ;;    • base-patch           : Position de la base
    ;;    • last-statu           : Statu avant le rapatriement
    set beliefs ([])
    intelligent-update-belief "target" nobody
    intelligent-update-belief "etat enrollement" "cherche convoi"
    intelligent-update-belief "counter enrollement" 0
    intelligent-update-belief "drone-protect leader" nobody
    intelligent-update-belief "drone-protect team" []
    
    intelligent-update-belief "base-patch" nobody
    intelligent-update-belief "last-statu" "enBase"
    
    ;; Deplacement
    set speed speed-drones * simu-speed / 1000
    set max-dir 10 * simu-speed
    set max-alt max-alt-drones + solAlt
    set min-alt min-alt-drones + solAlt
    
    ;; Carburant                             --- Plusieurs modèles de drones avec des caractéristiques pré-définis ?
    set fuel max-fuel-drones
    set max-fuel max-fuel-drones
    set pourcentage-securite-fuel-drones 0.2
    
    ;; Placement
    move-to one-of patches with [ base? ]
    set zcor solAlt
    ifelse random 2 = 0
    [ set heading 0 ]
    [ set heading 90 ]
    
    ;; Stratégie
    add-intention "cherche-convoi-drones" "cherche-convoi-drones-done"
    add-intention "take-off-drones" "take-off-drones-done"
  ]
end

to drones-think
  ;; Les drones cherchent un chemin jusqu'à leur target
  ;; s'il y a un obstable
  search-ways-drones
  
  ask drones with [ not dead? and fuel >= 0 ] [
    ;; Connaissances
    send-knowledges-drones
    
    let new-knowledge get-msg-if-performative "knowledge"
    while [new-knowledge != nobody]
    [
      if new-knowledge != nobody
      [
        update-knowledges-drones new-knowledge
      ]
      set new-knowledge get-msg-if-performative "knowledge"
    ]
    
    execute-intentions
    
    check-fuel-drones
    
    ;; Coût du flottement
;    if zcor > solAlt + 0.5 [ fuel-consomme "floating" 1 ]
    
;    if ticks mod 15 < 15
;    [
;      show-debug intelligent-get-belief "K-Infos-Drones"
;      show-debug intelligent-get-belief "K-Infos-Convois"
;    ]
  ]
  
  ask drones with [ (dead? or fuel <= 0) and zcor > solAlt ] [
    set color black
    ifelse solAlt > 0.2
    [ set zcor zcor - 0.2 ]
    [ set zcor 0 ]
  ]
end