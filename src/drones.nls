;;;;;;;;;;;;;;;;;;;;;;;;;
;; Drones related code ;;
;;;;;;;;;;;;;;;;;;;;;;;;;

breed [drones drone]

drones-own [
  ;; Statut du drone
  ;; ---
  ;; • statu                       : Le poste que doit tenir le drone parmis
  ;;    • protect-convoi           : Le drone protege un convoie
  ;;    • attaquant                : Le drone attaque les ennemis
  ;;    • ravitailleur             : Le drone ravitaille les autres drones
  ;;    • surveillant              : Le drone surveille le terrain
  ;;
  ;; • initial-health              : Nombre de points de vie initiaux du drone
  ;; • health                      : Nombre de points de vie restants au drone
  ;; • health                      : Nombre de points de vie restants au drone au tick dernier
  ;; • leader?                     : Grade du drone
  ;; • dead?                       : Vitalité du drone
  ;; • isAttacked                  : Indique si le drone est attaqué
  ;; • vision-radius               : Taille du champs de vision
  ;; • attacker                    : Est-ce que le drone est attquant ?
  ;; • ammos                       : Munitions du drone
  ;; • ammos-degat                 : Dégats d'un tire
  ;;
  ;; • intentions                  : Liste des intentions du drone, pour la BDI
  ;; • incoming-queue              : Liste des messages reçus
  ;; 
  ;; • K-drones-updated            : Indique s'il y a eu une modification apporter à ses connaissances concernant les drones
  ;; • K-convois-updated           : Indique s'il y a eu une modification apporter à ses connaissances concernant les convois
  ;; • K-enemies-updated           : Indique s'il y a eu une modification apporter à ses connaissances concernant les ennemis
  ;; • last-role                   : Role du drone au dernier tick
  ;; • last-isAttacked             : Indique si le drone était attaqué au dernier tick
  ;; • last-stat-of-fuel           : Donne l'état du niveau de carburant du dernier tick (cf. partage-connaissances.nls)
  ;; • last-tick-update            : Dernier tick ou le convoi à changer de configuration
  ;; • last-call-for-get-receivers : Dernière liste engendrée par la fonction get-receivers
  ;; • last-ammos                  : Dernier état des munitions du drone
  statu
  initial-health
  health
  last-health
  leader?
  dead?
  isAttacked
  vision-radius
  attacker
  ammos
  ammos-degat
  
  intentions
  incoming-queue
  
  K-drones-updated
  K-convois-updated
  K-enemies-updated
  last-role
  last-isAttacked
  last-stat-of-fuel
  last-tick-update
  last-call-for-get-receivers
  last-ammos

  ;; Beliefs
  ;; ---
  ;;   Connaissances
  ;;   ---
  ;;   • K-Infos-Drones           : Liste des connaissances sur les autres drones
  ;;   • K-Infos-Convois          : Liste des connaissances sur les autres convois
  ;;   Suivi
  ;;   ---
  ;;   • target                   : Convoi que le drone suit et protège
  ;;   • convoi-already-protected : Liste des convois qui ont une protection suffisante
  ;;   • drone-protect leader     : Leader du groupe de protection
  ;;   • drone-protect team       : Equipe de protection
  ;;   Rapatriement
  ;;   ---
  ;;   • base-patch               : Position de la base
  ;;   • last-statu               : Statu avant le rapatriement
  beliefs

  ;; Déplacement
  ;; ---
  ;; • speed            : Vitesse du drone
  ;; • max-dir          : Rotation maximale par tick
  ;; • max-alt          : Altitude de vol maximale
  ;; • min-alt          : Altitude de vol minimale
  ;; • my-paht          : Chemin que doit suivre le drone
  speed
  max-dir
  max-alt
  min-alt
  my-path
  
  ;; Carburant
  ;; ---
  ;; • fuel             : Carburant restant
  ;; • max-fuel         : Taille du réservoire
  ;; • pourcentage-sec. : Pourcentage de fuel suplémentaire estimé comme nécessaire pour un Rapatriement
  fuel
  max-fuel
  pourcentage-securite-fuel-drones
  
  ;; Munitions
  ;; ---
  ;; • elapsed-time     : Temps écoulé depuis le dernier tire
  ;; • reload-time      : Temps de chargement de l'arme entre deux tirs
  elapsed-time
  reload-time
]

;;;;;;;;;;;
;; SETUP ;;
;;;;;;;;;;;

to setup-drones
  create-drones nb-drones
  load-shapes-3d "ressources/drone.nlshape"
  
  let tmp-nb-protector nb-protector
  let tmp-nb-explorator nb-explorator
  
  ask drones [
    ; Init apparence NetLogo
    set shape "turtle"
    set size size * 0.03
    ;set color grey - 2
    
    ;; Variables
    ;; ---
    ;; Statut du drone
    set initial-health max-health-drones
    set health initial-health
    set leader? false
    set dead? false
    set isAttacked false
    set vision-radius drones-vision-radius
    set attacker false
    set ammos ammos-drones
    set ammos-degat ammos-degats-drones
    
    set intentions ([])
    set incoming-queue []
    
    set K-drones-updated false
    set K-convois-updated false
    set K-enemies-updated false
    set last-isAttacked false
    set last-tick-update 0
    set last-call-for-get-receivers []
    set last-ammos ammos
    
    ;; Beliefs
    ;; ---
    ;;   Connaissances
    ;;   ---
    ;;   • K-Infos-Drones            : Liste des connaissances sur les autres drones
    ;;   • K-Infos-Convois           : Liste des connaissances sur les autres convois
    ;;   Suivi
    ;;   ---
    ;;    • target                   : Convoi que le drone suit et protège
    ;;    • convoi-already-protected : Liste des convois qui ont une protection suffisante
    ;;    • etat enrollement         : Etape dans la procédure de recrutement
    ;;    • counter enroll.          : Compteur servant au TimeOut pour la procédure de recrutement
    ;;   Rapatriement
    ;;   ---
    ;;    • base-patch               : Position de la base
    ;;    • last-statu               : Statu avant le rapatriement
    set beliefs ([])
    setup-drones-beliefs
    
    ;; Deplacement
    set speed speed-drones * simu-speed / 1000
    set max-dir 10 * simu-speed
    set max-alt max-alt-drones + solAlt
    set min-alt min-alt-drones + solAlt
    set my-path []
    
    ;; Carburant                             --- Plusieurs modèles de drones avec des caractéristiques pré-définis ?
    set fuel max-fuel-drones
    set max-fuel max-fuel-drones
    set pourcentage-securite-fuel-drones 0.2
    set last-stat-of-fuel floor (fuel / fuel-estimate)
    
    
    ;; Munitions
    set elapsed-time 0
    set reload-time drones-reload-time
    
    ;; Placement
    move-to one-of patches with [ base? ]
    set zcor solAlt
    ifelse random 2 = 0
    [ set heading 0 ]
    [ set heading 90 ]
    
    ;; Stratégie
    ifelse tmp-nb-protector > 0
    [
      set statu "protect-convoi"
      add-intention "cherche-convoi-drones" "cherche-convoi-drones-done"
      add-intention "take-off-drones" "take-off-drones-done"
      set last-role intention-name get-intention
      set tmp-nb-protector tmp-nb-protector - 1
    ]
    [
      if tmp-nb-explorator > 0
      [
        set statu "explorer"
        intelligent-update-belief "etat terrain" setup-etat-terrain-drones
        add-intention "explore-drones" "explore-drones-done"
        add-intention "take-off-drones" "take-off-drones-done"
        set last-role intention-name get-intention
        set tmp-nb-explorator tmp-nb-explorator - 1
      ]
    ]
  ]
end

to setup-drones-beliefs
  intelligent-update-belief "role" "drone-protect"
  intelligent-update-belief "K-Infos-Drones" []
  intelligent-update-belief "K-Infos-Convois" []
  intelligent-update-belief "K-Infos-Enemies" []
  intelligent-update-belief "K-receivers" []
  intelligent-update-belief "target" nobody
  intelligent-update-belief "convoi-already-protected" []
  intelligent-update-belief "etat enrollement" "cherche convoi"
  intelligent-update-belief "counter enrollement" 0
  intelligent-update-belief "drone-protect leader" nobody
  intelligent-update-belief "drone-protect team" []
  
  intelligent-update-belief "old-target" nobody
  intelligent-update-belief "explorator-check-counter" 0
  
  intelligent-update-belief "base-patch" nobody
  intelligent-update-belief "last-statu" "enBase"
  intelligent-update-belief "etat terrain" setup-etat-terrain-drones
  intelligent-update-belief "explorer center" nobody
end

to soft-reset-drones
  ask drones [
    ; Init apparence NetLogo
    
    ;; Variables
    ;; ---
    ;; Statut du drone
    set statu "protect-convoi"
    set intentions ([])
    set incoming-queue []
    
    set dead? false
    set vision-radius drones-vision-radius
    
    ;; Beliefs
    ;; ---
    ;;   Suivi
    ;;   ---
    ;;    • target               : Convoi que le drone suit et protège
    ;;    • etat enrollement     : Etape dans la procédure de recrutement
    ;;    • counter enroll.      : Compteur servant au TimeOut pour la procédure de recrutement
    ;;    • drone-protect leader : Leader du groupe de protection
    ;;    • drone-protect team   : Equipe de protection
    ;;   Rapatriement
    ;;   ---
    ;;    • base-patch           : Position de la base
    ;;    • last-statu           : Statu avant le rapatriement
    set beliefs ([])
    intelligent-update-belief "target" nobody
    intelligent-update-belief "etat enrollement" "cherche convoi"
    intelligent-update-belief "counter enrollement" 0
    intelligent-update-belief "drone-protect leader" nobody
    intelligent-update-belief "drone-protect team" []
    
    intelligent-update-belief "base-patch" nobody
    intelligent-update-belief "last-statu" "enBase"
    
    ;; Deplacement
    set speed speed-drones * simu-speed / 1000
    set max-dir 10 * simu-speed
    set max-alt max-alt-drones + solAlt
    set min-alt min-alt-drones + solAlt
    
    ;; Carburant                             --- Plusieurs modèles de drones avec des caractéristiques pré-définis ?
    set fuel max-fuel-drones
    set max-fuel max-fuel-drones
    set pourcentage-securite-fuel-drones 0.2
    
    ;; Placement
    move-to one-of patches with [ base? ]
    set zcor solAlt
    ifelse random 2 = 0
    [ set heading 0 ]
    [ set heading 90 ]
    
    ;; Stratégie
    add-intention "cherche-convoi-drones" "cherche-convoi-drones-done"
    add-intention "take-off-drones" "take-off-drones-done"
  ]
end

to drones-think
  ;; Les drones cherchent un chemin jusqu'à leur target
  ;; s'il y a un obstable
  search-ways-drones
  
  ask drones with [ not dead? and fuel >= 0 ] [
    ;; Le drone observe les alentours
    ;; Les convois --- enlevé
    ;; Les ennemis
    ;observe-convois-drones
    observe-enemies
    
    ;; Traitement des morts
    traite-dead
    
    ;; Connaissances
    traite-connaissances
    
    ;; Exécution des comportements
    execute-intentions
    
    ;; Vérifie s'il est attaqué
    check-attacked
    
    ;; Vérification du carburant restant
    check-fuel-drones
    
    ;; Mise en place de la mémoire temporelle
    set last-role intention-name get-intention
    set last-isAttacked isAttacked
    set last-stat-of-fuel floor (fuel / max (list fuel-estimate 50))
  ]
  
  ;; Déctectino des drones morts et des drones sans carburant
  ask drones with [ (dead? or fuel <= 0) and zcor > solAlt ] [
    set color black
    ifelse solAlt > 0.2
    [ set zcor zcor - 0.2 ]
    [ set zcor 0 ]
  ]
end

;; msg-to-trash-drones
;; ---
;; Efface tous les messages qui ne sont pas interessants
to msg-to-trash-drones
  if statu = "explorer"
  [
    while [ remove-msg-if-performative (list "drone-protect") ] []
  ]
  
  if statu = "protect-convoi"
  [
    
  ]
end