;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Enemies related code ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;

breed [enemies enemy]

enemies-own [
  ;; Statut de l'ennemi
  ;; ---
  ;; • munitons             :  Nombre de munitions que possède l'ennemi
  ;; • carburant            : Le drone suit le convoi
  ;; • vision-radius        : Le drone n'a plus assez de carburant
  ;; • shoot-radius         : Plus de fuel, ni de moyen de se ravitailler
  ;; • speed                : Vitesse de déplacement de l'ennemi
  ;; • max-dir              : Angle de rotation maximal par tour d'un ennemi
  ;; • my-path              : Plus de fuel, ni de moyen de se ravitailler
  ;; • dead?                : Plus de fuel, ni de moyen de se ravitailler
  munitions
  carburant
  vision-radius
  shoot-radius
  speed
  max-dir
  my-path
  dead?
  
  ;; • hp                   : Points de vie de l'ennemi
  ;; • elapsed-time         : temps écoulé depuis dernier tir
  ;; • reload-time          : Temps de chargement de l'arme entre deux tirs
  hp
  elapsed-time
  reload-time
  
  ;; Variables pour BDI
  ;; ---
  ;; • beliefs              : Ensemble des croyances de l'ennemi
  ;; • intentions           : Ensemble des intentions de l'ennemi
  beliefs
  intentions
]


;;;;;;;;;;;
;; SETUP ;;
;;;;;;;;;;;

to setup-enemies
  
  ; Création des ennemis
  create-enemies nb-enemies
  
  ask enemies [
    ;; INITIALISATION des variabes que possède chaque ennemi
    ;; ---
    set munitions (average-munition-enemies - 5 + random 10)
    set carburant (average-carburant-enemies - 5 + random 10)
    set vision-radius enemies-vision-radius
    set shoot-radius enemies-shoot-radius
    set speed 0.05 * simu-speed
    set max-dir 10 * simu-speed
    set my-path []
    set dead? false
    
    set hp enemies-health-points
    set elapsed-time enemies-reload-time
    set reload-time enemies-reload-time
    
    set beliefs []
    set intentions []
    
    ;; ASPECT
    set shape "truck"
    set color red - 2
    
    ;; PLACEMENT
    ;; Au hasard dans le carré nord-ouest
    move-to one-of patches with [not obstacle? and not objectif? and not base? and not hangar? and pxcor < (max-pxcor / 2) and pycor >= (max-pycor / 2) and pzcor = solAlt ]
    set zcor solAlt
  ]
end


;;;;;;;;;;
;; MOVE ;;
;;;;;;;;;;

; Avance devant à la vitesse de l'ennemi, mais seulement si on le peut (pas d'obstacle)
to avancer
  ;; Vérification du patch en mire, s'il n'y a pas un agent ou d'obstacle
  ifelse (detect-agent-ahead or detect-obstacles-ahead)
    [
      rt max-dir / 2 + random (max-dir / 2)
    ]
    [
      fd speed
    ]
end

; Mouvement randomisé, principalement pour le début de la simulation
to random-move-nowrap
  ;; Changement de cap selon probabilité
  if random 100 < 5 
  [ lt random max-dir * one-of [ 1 -1 ] ] ;; Choix vers la gauche ou la droite
  avancer
end

; Mouvement vers un patch spécifique, principalement utilisé pour le mouvement astar
to move-to-patch [ patch-target ]
  rotate-agent patch-target
  fd speed
end

; Mouvement le long d'un chemin astar calculé précédemment
to move-to-target-astar
  ; Vérification qu'on a bien un chemin astar en mémoire
  if not empty? my-path 
  [
    let next-patch first my-path
    let zz pzcor
    set next-patch [patch-at 0 0 (zz - pzcor)] of next-patch ; mise a niveau de pzcor au cas ou le chemin a ete calculé sur un autre plan
                                                             ; Deplacement vers le prochain patch
    if next-patch != patch-here [move-to-patch next-patch]
    ; Si on a atteint ce patch, on le supprime de la liste, et on va donc continuer vers le prochain patch du chemin
    if patch-here = next-patch 
    [
      set my-path remove-item 0 my-path
      if debug [ show (word "Waypoint atteint: " patch-here ", prochain: " next-patch ) ]
    ]
  ]
end

; Lorsqu'on a repéré un convoi, on fonce sur lui pour tirer dessus
to move-to-target
  rotate-agent intelligent-get-belief "target"
  
  ; On avance
  if distance intelligent-get-belief "target" > enemies-shoot-radius / 3
    [
      ifelse not detect-agent-ahead
      [ fd speed ]
      [ rt max-dir / 2 + random (max-dir / 2) ]
    ]
end

to shoot-target
  set elapsed-time elapsed-time + 1
  if elapsed-time > reload-time [
    if distance intelligent-get-belief "target" < shoot-radius
    [
      sound:play-note "GUNSHOT" 60 64 1
      set elapsed-time 0
      
      if random 100 < ammos-accuracy [ ask intelligent-get-belief "target" [ set hp hp - ammos-degats ] ]
    ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;
;; Fonctions diverses ;;
;;;;;;;;;;;;;;;;;;;;;;;;

; détecte si une turtle se trouve entre l'ennemi et la case où il veut aller
to-report detect-agent-ahead
  report detect-agent-between-target patch-at-heading-and-distance heading (speed + safe-distance)
end

; Détecte les obstacles entre la turtle et le patch où il doit se rendre devant lui
to-report detect-obstacles-ahead
  report detect-obstacle-between-target patch-at-heading-and-distance heading (speed + safe-distance)
end

; Détecte les obstacles entre la turtle et le patch où se trouve la target
to-report detect-obstacles-enemy-target
  if [obstacle?] of [patch-here] of intelligent-get-belief "target" or [obstacle?] of patch-here
  [
    report true
  ]
  ; on stocke le heading de la turtle pour la reset quand on a fini
  let headingFlag heading
  face-nowrap intelligent-get-belief "target"
  
  ; Vérification que les patchs entre la turtle et le patch visé ne sont pas des obstacles
  let tmp 1
  let tmpbool false
  while [ tmp <= distance intelligent-get-belief "target" and tmpbool = false ]
  [
    set tmpbool [obstacle?] of patch-ahead tmp
    set tmp tmp + 1
  ]
  
  ; Remise à la direction de départ
  set heading headingFlag
  
  report tmpbool
end

; Détection du convoi le plus proche pour l'assigner à la target
to detect-convois
  ; Récupération des convois dans le champ de vision
  ; let targets convois in-radius-nowrap enemies-vision-radius
  let targets detect-convois-in-vision
  
  ; Mise à jour des connaissances
  ifelse empty? targets [
    ; Si on connaissait la position d'un convoi à viser, on la supprime et on recommence à chercher
    if exists-belief-target
    [
      remove-belief "target"
    ]
  ]
  [
    ; Mise à jour des connaissances
    intelligent-update-belief "target" (first targets)
    
    ;show intentions
  ]
end

; Decide d'un chemin a suivre si l'ennemi a une cible
to search-ways-for-enemies
  ; Pour chaque ennemi avec cible et qui n'est pas mort, on choisit un chemin
  foreach sort-on [who] turtle-set enemies with [not dead? and exists-belief-target and current-intention = "eviter-obstacle"] [
    let id ([who] of ?)
    ; On ne calcule un chemin QUE si c'est nécessaire : obstacle entre soi et la target
    if empty? [my-path] of ?
    and not [obstacle?] of [patch-here] of [intelligent-get-belief "target"] of ? [ ; s'il n'y a pas encore de chemin du tout, ou pas de chemin pour cette voiture, on cree un plan AStar
                                                                                    ; Cree le plan AStar (attention a ca que le patch start soit au niveau ou il y a les obstacles, ici pzcor = mapAlt pour les obstacles)
      let start-patch min-one-of (patches with [pzcor = mapAlt and not obstacle?]) [distance ?] ; on s'assure de choisir comme patch de depart un patch libre sans obstacle, sinon quand on split un convoi il se peut qu'il soit sur un obstacle et qu'il ne puisse jamais generer de chemin
      let path (plan-astar ([patch-at 0 0 (pzcor * -1)] of start-patch) [patch-here] of [intelligent-get-belief "target"] of ? false)
      ask ? [ set my-path path ]
    ]
  ]
end

to enemies-think
  ; Decide d'un chemin a suivre si l'ennemi a une cible
  search-ways-for-enemies
  
  ask enemies [
    execute-intentions
    update-intentions
    if show-intentions and empty? intentions [ show intentions ]
    ;    shoot-target
  ]
end

;;;;;;;;;;;;;
;; BELIEFS ;;
;;;;;;;;;;;;;

; Indique que l'ennemi a un belief "target"
to-report exists-belief-target
  report exist-beliefs-of-type "target"
end

;;;;;;;;;;;;;;;;
;; INTENTIONS ;;
;;;;;;;;;;;;;;;;

;; Liste des intentions possibles de l'ennemi
;; ---

; Rechercher une target, car on n'en a pas encore
to chercher-target
  detect-convois
  random-move-nowrap
end

; On a repéré une target et il n'y a pas d'obstacle entre elle et nous, donc on fonce
to suivre-target
  detect-convois
  move-to-target
  shoot-target
end

; On a repéré une target et il y a un obstacle, on suit astar
to eviter-obstacle
  detect-convois
  move-to-target-astar
  shoot-target
end

;; Fonctions sur les intentions
;; ---

; Mise à jour des intentions selon les situations
to update-intentions
  ; Sans target, on en cherche une, sinon on la poursuit
  ifelse not exists-belief-target and empty? my-path
  [
    set my-path []
    if empty? intentions or current-intention != "chercher-target"
    [ add-intention "chercher-target" "exists-belief-target" ]
  ]
  [
    if exists-belief-target
    [
      let obstacle-ahead detect-obstacles-enemy-target
      if not obstacle-ahead and (empty? intentions or current-intention != "suivre-target")
      [
        ;if (not empty? intentions) and current-intention != "suivre-target" [pop-intention]
        set my-path []
        add-intention "suivre-target" "detect-obstacles-enemy-target"
      ]
      if obstacle-ahead and (empty? intentions or current-intention != "eviter-obstacle")
      [
        ;if (not empty? intentions) and current-intention != "eviter-obstacle" [pop-intention]
        add-intention "eviter-obstacle" "not detect-obstacles-enemy-target"  
      ]
    ]
    
  ]
end
